ext {
    SONATYPE_URL = ""
    IS_DEV_BUILD = false
    IS_RELEASE_BUILD = false
    GROUP = "com.reveldigital"
}

version = 1.2
group = GROUP

if (hasProperty("release")) {
    IS_RELEASE_BUILD = true
    SONATYPE_URL = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
} else if (hasProperty("snapshot")) {
    version += "-SNAPSHOT"
    SONATYPE_URL = "https://oss.sonatype.org/content/repositories/snapshots/"
} else {
    IS_DEV_BUILD = true
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

jar {
    baseName = 'reveldigital-api'
    version = version
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile 'com.google.code.gson:gson:2.3'
}

signing {
    required { IS_RELEASE_BUILD && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        if (IS_DEV_BUILD) {
            mavenLocal()
        } else {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: SONATYPE_URL) {
                    authentication(userName: mavenUsername, password: mavenPassword)
                }

                pom.project {
                    name 'RevelDigital API'
                    packaging 'jar'
                    description 'Wrapper classes for interacting with RevelDigital HTTP/JSON API.'
                    artifactId = "reveldigital-api"
                    groupId GROUP
                    version version
                    url 'https://github.com/RevelDigital/reveldigital-api'

                    scm {
                        url 'scm:git@github.com:RevelDigital/reveldigital-api.git'
                        connection 'scm:git@github.com:RevelDigital/reveldigital-api.git'
                        developerConnection 'scm:git@github.com:RevelDigital/reveldigital-api.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'mtinnes'
                            name 'Mike Tinnes'
                            email 'mtinnes@catalystllc.biz'
                        }
                    }
                }
            }
        }
    }
}
build.mustRunAfter clean
